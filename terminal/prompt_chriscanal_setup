# Simple zsh prompt.
#
# dotfiles ❯ (default)
# dotfiles master ❯ (in git repository)
# root@serv dotfiles master ❯ (with SSH)
#
# * is prepended to git branch name if repo is dirty.
# ❯ is green or red depending on previous command exit status.
#
# Author: Paul Miller (paulmillr.com)

vcs_info=''
nvm_info=''

function prompt_chriscanal_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
}

function list-files {
  echo `script -q /dev/null ls -G . | tr -d '\r' | cat`
}

function get-vcs-info {
  vcs_info=''
  git rev-parse --is-inside-work-tree &>/dev/null || return

  local ref=$(git symbolic-ref -q HEAD | sed -e 's|^refs/heads/||')
  if [[ -z "$ref" ]]; then
    vcs_info=''
  else
    st=`git diff --quiet --ignore-submodules HEAD &>/dev/null; [ $? -eq 1 ] && echo ' %F{red}✘' || echo ' %F{green}✔'`
    vcs_info=" %F{blue}%F{magenta}${ref}${st}%f"
  fi
}

function get-nvm-info {
  nvm_info=''

  if [ ! -f package.json ]; then
    return
  else
    [ -f "$HOME/.nvm/nvm.sh" ] || return
    local nvm_prompt
    nvm_prompt=$(node -v 2>/dev/null)
    [[ "${nvm_prompt}x" == "x" ]] && return
    nvm_prompt=${nvm_prompt:1}

    local ver engine
    engine=$(cat package.json | jsawk 'return this.engines && this.engines["node"]')

    local nvm_color='036'

    if [ ! -z $engine ]; then
      ver=`semver ${nvm_prompt} -r ${engine}`
    else
      ver='yup'
      nvm_color='130'
    fi

    if [[ -z "$ver" ]]; then
      nvm_color='red'
    fi

    nvm_info=" %F{${nvm_color}}%B⬡%b %F{${nvm_color}}${nvm_prompt}%f"

  fi
}

function prompt_chriscanal_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook
  add-zsh-hook precmd get-vcs-info
  add-zsh-hook precmd get-nvm-info
  add-zsh-hook chpwd list-files
  add-zsh-hook chpwd get-vcs-info
  add-zsh-hook chpwd get-nvm-info

  # zstyle ':omz:module:editor' completing '%B%F{red}...%f%b'

  # Time: %F{yellow}%T
  PROMPT='%F{yellow}%T${nvm_info} %F{magenta}${SSH_TTY:+%n@%m }%F{cyan}%1~%f${vcs_info}\
%(!.%B%F{red}#%f%b.%B %(?.%F{green}.%F{red})❯%f%b) '
  RPROMPT=''
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_chriscanal_setup "$@"

